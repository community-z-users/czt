/**
Copyright 2003 Tim Miller
This file is part of the CZT project.

The CZT project contains free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2 of the License, or
(at your option) any later version.

The CZT project is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with CZT; if not, write to the Free Software
Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
*/
package net.sourceforge.czt.parser.oz;

import java.io.*;
import java.math.BigInteger;
import java.util.ArrayList;
import java.util.Iterator;
import java.util.List;

import java_cup.runtime.*;

import net.sourceforge.czt.base.ast.*;
import net.sourceforge.czt.z.ast.*;
import net.sourceforge.czt.oz.ast.*;
import net.sourceforge.czt.oz.impl.OzFactoryImpl;
import net.sourceforge.czt.util.CztException;
 
action code
{:
    /** The name of the mathematical toolkit */
    public final static String TOOLKIT = "standard\\_toolkit";

    /** The symbols used to replace arguments for names in ApplExpr etc. */
    public final static String ARG_TOK = " _ ";
    public final static String LISTARG_TOK = " _ ";
    public final static String SPACE = " ";

    /** The two types of name */
    protected final static int DECLNAME = 1;
    protected final static int REFNAME = DECLNAME + 1;

    /** The factory for creating AST objects. */
    protected OzFactory ozFactory_ = new OzFactoryImpl();

    /** The table of operators. */
    protected OperatorTable table_;

    /** The "fix" of the current operator being parsed. */
    protected int fix_ = -1;

    /** The type of the previous chain relation e.g. MEM, EQUALS, IP */
    protected int previousChain_ = -1;

    /**
     * Create an empty list
     */
    protected List list()
    {
      return new ArrayList();
    }

    /**
     * Create a list containing one element
     */
    protected List list(Object o)
    {
      List result = new ArrayList();
      result.add(o);
      return result;
    }

    /**
     * Create a list containing two elements
     */
    protected List list(Object o1, Object o2)
    {
      List result = new ArrayList();
      result.add(o1);
      result.add(o2);
      return result;
    }


    /**
     * Create a Name from a string, extracting out the
     * appropriate decoration tokens, and return a DeclName
     * or RefName as specified
     */
    protected Name localCreateName(String name, int type)
    {
      Name result = null;

      List pair = LatexScanner.getNameAndStroke(name);
      String baseName = (String) pair.get(0);    
      List strokes = (List) pair.get(1);

      for (int i = 0; i < strokes.size(); i++) {

        int sym = ((Symbol) strokes.get(i)).sym;

        Stroke stroke = null;
        switch (sym) {
          case LatexSym.INSTROKE:
            stroke = ozFactory_.createInStroke();
            break;
          case LatexSym.OUTSTROKE:
            stroke = ozFactory_.createOutStroke();
            break;
          case LatexSym.NEXTSTROKE:
            stroke = ozFactory_.createNextStroke();
            break;
          case LatexSym.NUMSTROKE:
            Integer iStroke = (Integer) ((Symbol) strokes.get(i)).value;
            stroke = ozFactory_.createNumStroke(iStroke);
            break;
          default:
            //TODO: throw exception
        }
        strokes.set(i, stroke);
      }

      if (type == DECLNAME) {
        result = ozFactory_.createDeclName(baseName, strokes, null);
      }
      else {
        result = ozFactory_.createRefName(baseName, strokes, null);
      }
      return result;
    }

    /**
     * Create a DeclName from a string, extracting out the
     * appropriate decoration tokens
     */
    protected DeclName localCreateDeclName(String name) {
      return (DeclName) localCreateName(name, DECLNAME);
    }

    /**
     * Create a RefName from a string, extracting out the
     * appropriate decoration tokens
     */
    protected RefName localCreateRefName(String name) {
      return (RefName) localCreateName(name, REFNAME);
    }

    /**
     * Converts a list of strings into one long string with spaces between
     * each token
     */
    protected String strListToString(List list)
    {
      String name = new String();
      String previous = new String();

      for (Iterator iter = list.iterator(); iter.hasNext(); ) {
        String next = (String) iter.next();
        if ((!next.equals(ARG_TOK) || !next.equals(LISTARG_TOK)) &&
            (!previous.equals(ARG_TOK) || !previous.equals(LISTARG_TOK))) {
          name += SPACE;
        }
        name += next;
        previous = next;
      }
      return name;        
    }

    /**
     * Converts a list of String objects to a DeclName object
     */
    protected DeclName strListToDeclName(List list)
    {
      return ozFactory_.createDeclName(strListToString(list), list(), null);
    }

    /**
     * Converts a list of String objects to a RefName object
     */
    protected RefName strListToRefName(List list)
    {
      return ozFactory_.createRefName(strListToString(list), list(), null);
    }

    /**
     * Converts a Term to a Pred
     */
    protected Pred pred(Term t)
    {
      Pred result = null;

      if (t instanceof Pred) {
        result = (Pred) t;
      }
      else if (t instanceof Expr) {
        result = ozFactory_.createExprPred((Expr) t);
      }
      else {
        //I don't think that this is possible...?
        exception("Predicate expected, but read: " + t.getClass().getName());
      }
      return result;
    }

    /**
     * Converts an Term to an Expr
     */
    protected Expr expr(Term t)
    {
      Expr result = null;

      if (t instanceof Expr) {
        result = (Expr) t;
      }
      else if (t instanceof ExprPred) {
        result = ((ExprPred) t).getExpr();
      }
      else {
        exception("Expression expected, but read: " + t.getClass().getName());
      }
      return result;
    }

    /**
     * Converts a Term to a RefName
     */
    protected RefName refname(Term t)
    {
      RefName result = null;

      if (t instanceof RefName) {
        result = (RefName) t; 
      }
      else if (t instanceof RefExpr) {
        RefExpr re = (RefExpr) t;

        if (re.getExpr().size() > 0) {
          exception("RefName with no generic instantiation list expected");
        }
        result = re.getRefName();
      }
      else {
        exception("RefName expected, but read: " + t.getClass().getName());
      }

      return result;
    }

    /**
     * Throws a CztException with the specified error message
     */
    protected void exception(String message)
    {
       throw new CztException(message);
    }

    /**
     * Throws a CztException with the specified error message and 
     * nested throwable
     */
    protected void exception(String message, Throwable cause)
    {
       throw new CztException(message, cause);
    }

    /**
     * Creates a member predicate from a string list and an expression list
     */
    protected MemPred localCreateMemPred(List name, List exprList)
    {
      TupleExpr te = ozFactory_.createTupleExpr(exprList);
      RefName rn = strListToRefName(name);
      RefExpr re = ozFactory_.createRefExpr(rn, list(), Boolean.FALSE);
      MemPred result = ozFactory_.createMemPred(te, re, Boolean.TRUE);
      return result;
    }

    /**
     * Creates an application expression from string string list and an 
     * expression list
     */
    protected ApplExpr localCreateApplExpr(List name, List exprList)
    {
      TupleExpr te = ozFactory_.createTupleExpr(exprList);
      RefName rn = strListToRefName(name);
      RefExpr re = ozFactory_.createRefExpr(rn, list(), Boolean.FALSE);
      ApplExpr result = ozFactory_.createApplExpr(te, re, Boolean.TRUE);
      return result;
    }

    /**
     * Returns the right hand side of the previous infix relation
     */
    protected Expr getPreviousRhsExpr(Pred pred)
    {
      Expr result = null;

      MemPred memPred = null;
      if (pred instanceof MemPred) {
        memPred = (MemPred) pred;
      }
      else {   //AndPred
        memPred = (MemPred) ((AndPred) pred).getRightPred();
      }

      if (previousChain_ == LatexSym.MEM) {
        result = memPred.getRightExpr();
      }
      else if (previousChain_ == LatexSym.EQUALS) {
        SetExpr se = (SetExpr) memPred.getRightExpr();
        result = (Expr) se.getExpr().get(0);
      }
      //previousChain_ == LatexSym.IP
      else {
        TupleExpr te = (TupleExpr) memPred.getLeftExpr();
        result = (Expr) te.getExpr().get(1);
      }

      return result;
    }

    /**
     * Convert a chain relation into a series of and predicates
     * (transformation rule 12.2.10.3)
     */
    protected Pred transformChainRel(Pred chain, int sym, Expr rhs)
    {
      Pred result = null;

      Expr lExpr = getPreviousRhsExpr(chain);
      Expr rExpr = rhs;

      if (sym == LatexSym.EQUALS) {
        rExpr = ozFactory_.createSetExpr(list(rhs));
      }
      
      MemPred mp = ozFactory_.createMemPred(lExpr, rExpr, Boolean.TRUE);
      result = ozFactory_.createAndPred(chain, mp, Op.Chain);

      previousChain_ = -1;
      return result;
    }

    protected Pred transformChainRelInfix(Pred chain, Expr rhs, String ip)
    {
      Pred result = null;

      List opName = list(ARG_TOK);
      opName.add(ip);
      opName.add(ARG_TOK);

      Expr lExpr = getPreviousRhsExpr(chain);
      Expr rExpr = rhs;

      MemPred mp = localCreateMemPred(opName, list(lExpr, rExpr));
      result = ozFactory_.createAndPred(chain, mp, Op.Chain);

      previousChain_ = -1;
      return result;
    }

    /**
     * Creates a list of strings for a list returned by expSep.
     * This list will represent the name of the operator to which
     * the expressions were applied
     */
    protected void addExpSepName(List opName, List es)
    {
      for (int i = 0; i < es.size(); i += 2) {

        List list = (List) es.get(i);
        if (list.size() == 1) {
          opName.add(ARG_TOK);
        }
        else {
          opName.add(LISTARG_TOK);
        }
        opName.add((String) es.get(i + 1));
      }
    }

    /**
     * Returns the list of expressions returned by expSep.
     */
    protected void addExpSepExp(List expList, List es)
    {
      for (int i = 0; i < es.size(); i += 2) {
        expList.addAll((List) es.get(i));
      }
    }
:}

parser code
{:
    /** The tokens that can have soft new lines both before and after */
    protected int [] nlBoth_ =
      new int [] {LatexSym.LEFTASSOC, LatexSym.PARENTS, LatexSym.BOXNAME,
        LatexSym.RIGHTASSOC, LatexSym.ELSE, LatexSym.THEN, LatexSym.DEFFREE,
        LatexSym.BAR, LatexSym.LDATA, LatexSym.ANDALSO,
        LatexSym.CONJECTURE, LatexSym.LISTARG, LatexSym.AND, LatexSym.OR,
        LatexSym.IMP, LatexSym.IFF, LatexSym.CROSS, LatexSym.SLASH,
        LatexSym.EQUALS, LatexSym.MEM, LatexSym.DEFEQUAL, LatexSym.COLON,
        LatexSym.COMMA, LatexSym.DOT, LatexSym.SEMICOLON, LatexSym.SPOT,
        LatexSym.ZHIDE, LatexSym.ZPROJ, LatexSym.ZCOMP, LatexSym.ZPIPE,
        LatexSym.I, LatexSym.IP, LatexSym.EL, LatexSym.ELP, LatexSym.ERE,
        LatexSym.EREP, LatexSym.ES, LatexSym.SS, LatexSym.SRE, LatexSym.SREP,
        LatexSym.VISIBILITY, LatexSym.INHERITS, LatexSym.PARALLEL,
        LatexSym.ASSOCPARALLEL, LatexSym.GCH, LatexSym.END,
        LatexSym.SCH, LatexSym.GENSCH, LatexSym.OPSCH, LatexSym.AX, 
        LatexSym.GENAX, LatexSym.ZED, LatexSym.CLASS, LatexSym.STATE, 
        LatexSym.INIT,
      };

    //in the ISO standard, "function", "generic", "relation", and
    //"section" are in the "both" category, but a new line before one of
    //these is possible if multiple paragraphs are permitted inside in
    //a zed paragraph, so have been moved into the "after" category
    /** The tokens that can only have soft new lines after */
    protected int [] nlAfter_ =
      new int [] {LatexSym.IF, LatexSym.LET, LatexSym.ZPRE,
        LatexSym.FUNCTION, LatexSym.GENERIC, LatexSym.RELATION, LatexSym.SECTION,
        LatexSym.LSQUARE, LatexSym.ARG, LatexSym.NOT, LatexSym.ALL,
        LatexSym.EXI, LatexSym.EXIONE, LatexSym.LPAREN, LatexSym.LBRACE,
        LatexSym.LBIND, LatexSym.LAMBDA, LatexSym.MU, LatexSym.THETA,
        LatexSym.PRE, LatexSym.PREP, LatexSym.L, LatexSym.LP,
        LatexSym.DCNJ, LatexSym.DGCH, LatexSym.DSQC,
      };

    //RDATA has been moved from "both" to "before"
    /** The tokens that can only have soft new lines before */
    protected int [] nlBefore_ =
      new int [] {LatexSym.RSQUARE, LatexSym.RPAREN, LatexSym.RBRACE,
        LatexSym.RBIND, LatexSym.POST, LatexSym.POSTP, LatexSym.ER,
        LatexSym.ERP, LatexSym.SR, LatexSym.SRP,
        LatexSym.RDATA
      };

    /** A symbol used for lookAhead */
    protected Symbol lookAhead_ = null;

    /** The previous symbol */
    protected Symbol previous_ = null;

    /** The table of operators */
    protected OperatorTable table_;

    /**
     * The constructor that receives the operator table
     */
    public LatexParser(Scanner scanner, OperatorTable table)
    {
      super(scanner);
      this.table_ = table;
    }

    /**
     * Returns true if and only if a symbol is in an array of symbols
     */
    protected boolean contains(int [] symbols, int symbol)
    {
      boolean result = false;

      for (int i = 0; i < symbols.length; i++) {
        if (symbols[i] == symbol) {
          result = true;
          break;
        }
      }
      return result;
    }

    /**
     * Return the next token, returning the operator symbol if it is a 
     * user defined operator
     */
    protected Symbol local_next_token() throws Exception
    {
        Symbol symbol = null;
        Symbol result = null;

        if (lookAhead_ == null) {
          symbol = getScanner().next_token();
        }
        else {
          symbol = new Symbol(lookAhead_.sym, lookAhead_.value);
          lookAhead_ = null;
        }
        result = localLookup(symbol);

        //remove any soft newline characters
        //TODO: Think about this... if the NL is at the end of a
        //prefix template declaration, and the next token is that
        //prefix name, the localLookup will return a DECORWORD instead of a
        //PRE or PREP. I don't think this should cause problems though
        if (result.sym == LatexSym.NL) {
          if (previous_ != null &&
              (contains(nlBoth_, previous_.sym) ||
              contains(nlAfter_, previous_.sym))) {

              return local_next_token();
          }
          else {
            lookAhead_ = localLookup(getScanner().next_token());
            if (lookAhead_.sym == LatexSym.NL) {
              return local_next_token();
            }
            else if (contains(nlBoth_, lookAhead_.sym) ||
                contains(nlBefore_, lookAhead_.sym)) {

                result = lookAhead_;
                lookAhead_ = null;
            }
          }
        }

        previous_ = result;
        return result;
    }

    /**
     * Lookup the value of this symbol
     */
    protected Symbol localLookup(Symbol symbol)
    {
       Symbol result = null;

       if (symbol.sym == LatexSym.DECORWORD || symbol.sym == LatexSym.DECLWORD) {
         List pair = LatexScanner.getNameAndStroke((String) symbol.value);
         String name = (String) pair.get(0);
         int type = table_.lookup(name);
         result = (type == -1) ? symbol : new Symbol(type, symbol.value);
       }
       else {
         result = symbol;
       }
       return result;
    }
:}

init with
{:
    //share the operator table between the action code and the parser code
    action_obj.table_ = table_;
:}

scan with {: return local_next_token(); :};

terminal String
        DELTA, THETA, MU, LAMBDA, POWER, TEXT,
        NEXTSTROKE, OUTSTROKE, INSTROKE,
        LPAREN, RPAREN, LSQUARE, RSQUARE, LBIND, RBIND,
        LDATA, RDATA, LBRACE, RBRACE,
        BAR, ANDALSO, CONJECTURE, AND, OR, IMP, IFF, NOT, 
        ALL, EXI, CROSS,
        SLASH, EQUALS, MEM, COLON, SEMICOLON,COMMA, DOT, SPOT, ZHIDE, 
        ZPROJ, ZCOMP, ZPIPE, NL,
        ZSECTION, SECTION, PARENTS, TRUE, FALSE, LET, IF, THEN, ELSE, ZPRE,
        RELATION, FUNCTION, GENERIC, LEFTASSOC, RIGHTASSOC, LISTARG, ARG,
        DEFFREE, DEFEQUAL, EXIONE,
        END, AX, SCH, GENSCH, GENAX, ZED, CLASS, STATE, INIT, INITWORD, OPSCH,
        VISIBILITY, INHERITS,
        DCNJ, DGCH, DSQC, PARALLEL, ASSOCPARALLEL, GCH,
        CLASSCOM, ENDCLASSCOM, CLASSCOMWORD,
        DECORWORD, DECLWORD, BOXNAME,
        PREP, PRE, POSTP, POST, IP, I, LP, L, ELP, EL, ERP, ER,
        SRP, SR, EREP, ERE, SREP, SRE, ES, SS,
        _APPLICATION, _RENAME;

terminal Integer NUMERAL, NUMSTROKE;

non terminal Spec
    specification;

non terminal List /*<ZSect>*/ sectionList;
non terminal ZSect section;

non terminal List /*<Parent>*/ parentList;
non terminal String sectionName;

non terminal List /*<Para>*/ 
    paragraphList,
    unboxedParagraphList,
    unboxedParagraphItemList;

non terminal Para 
    boxedParagraph,
    unboxedParagraphItem;

non terminal NarrPara narrParagraph;

non terminal List /*<String>*/
    optParents;

non terminal AxPara
    axiomaticDefinition,
    genericAxDefinition,
    abbreviationDefinition,
    schema;

non terminal ConstDecl
    letDefinition;

non terminal List /*<Freetype>*/ freeTypeList;
non terminal Freetype freeTypeDefinition;

non terminal List /*<String>*/
    optNarrWordList,
    narrWordList;

non terminal OptempPara operatorTemplate;
non terminal List /*<StringAndOperand?>*/ 
    template,
    prefixTemplate,
    postfixTemplate,
    infixTemplate,
    nofixTemplate,
    optWordOperandList;

non terminal Operand 
    varg,
    templateTag;
non terminal Assoc assoc;
non terminal OptempPara catTemplate;

non terminal SchText 
    schemaTextNoExpression,
    expressionSchemaText,
    schemaText;

non terminal List /*<ConstDecl>*/ letDefinitionList;

non terminal ClassPara
    class;

non terminal List /*<InheritedClass>*/ 
    inheritedList,
    inheritedClassList;
non terminal InheritedClass inheritedClass;
non terminal ActualParameters optActualParameters;
non terminal RenameExpr optRenameExpression;

non terminal RefNameList 
    deltaList,
    visibilityList;
                        
non terminal FormalParameters classFormalParameters;

non terminal List /*<Branch>*/ branchList;
non terminal Branch branch;

non terminal List /*<Operation>*/ operationList;

non terminal List /*<Para>*/
    localDefinitionList,
    unboxedLocalDefinitionBlock,
    unboxedLocalDefinitionList;

non terminal AxPara
    boxedLocalDefinition;

non terminal Para
    unboxedLocalDefinition;

non terminal LocalDef
    localDefinitionBlock;

non terminal List /*<DeclName>*/ 
    nameList,
    declWordList,
    declNameList;
non terminal DeclName
    name,
    boxName,
    declName;

non terminal List /*<RefName>*/ refNameList;
non terminal RefName refName;

non terminal List /*<NameNamePair>*/ renameList;
non terminal NameNamePair rename;
    
non terminal List /*<Decl>*/ 
    optDeclPart,
    declPart;
non terminal Decl 
    basicDeclaration,
    declaration;

non terminal GivenPara basicTypeDefinition;

non terminal State state;
non terminal List stateDecl;
non terminal SecondaryAttributes secondaryDecl;

non terminal InitialState initialState;

non terminal List /*<DeclName*/
    formalParameters,
    optFormalParameters;

non terminal List /*<Stroke>*/ optStrokeList;
non terminal Stroke stroke;

non terminal Op 
    optSep,
    sep,    
    optNL;

non terminal TermA
    term,
    func_appl,
    inner_term;

non terminal Pred
    predicate,
    predicate1;

non terminal MemPred
    prefixRel,
    postfixRel,
    nofixRel;

non terminal Pred
    relation,
    infixRel,
    infixChainRel;

non terminal Expr
    opPromotionLhs,
    expression;

non terminal List /*<NameExprPair*/
    optNameExprPairList,
    nameExprPairList;

non terminal NameExprPair nameExprPair;

non terminal List /*<Expr>*/ prodExpressionList;

non terminal List /*<Expr>*/ 
    optExpressionList,
    expressionList;

non terminal Operation operation;

non terminal OperationBox operationBox;
non terminal OperationExpr
    operationExpression,
    operationExpression1;

non terminal MainOpExpr mainOpExpression;

non terminal List /*<String>*/ 
    opName,
    prefixName,
    postfixName,
    infixName,
    nofixName,
    genName,
    prefixGenName,
    postfixGenName,
    infixGenName,
    nofixGenName;

non terminal List /*<String>*/
    optVargEsSsList,
    optNameEsSsList;

non terminal ApplExpr
    application,
    prefixApp,
    postfixApp,
    infixApp,
    nofixApp;

non terminal List /*<Object>*/
    expSep;

non terminal List /*<String>*/
    argErepOrSrep,
    argEreOrSre,
    argErOrSr,
    argEsOrSs,
    argErpOrSrp;

non terminal String
    ereOrSre,
    erOrSr,
    esOrSs;

non terminal String
    visibility;


precedence right        BAR;
precedence left         TEXT;
precedence left         RPAREN;
precedence left         NL, SEMICOLON;
precedence nonassoc     DEFEQUAL;
precedence left         ALL, EXI, EXIONE, SPOT;
precedence left         IFF;
precedence right        IMP;
precedence left         OR;
precedence left         AND;
precedence right        NOT;
precedence left         MEM, EQUALS, PREP, LP, IP, EREP, SREP, POSTP, ELP, ERP, SRP;
precedence right        LAMBDA;
precedence right        MU;
precedence right        LET;
precedence left         ASSOCPARALLEL;
precedence left         PARALLEL;
precedence left         GCH;
precedence right        ELSE;
precedence left         ZCOMP;
precedence left         ZPIPE;
precedence left         ZHIDE;
precedence left         ZPROJ;
precedence right        ZPRE;
precedence left         CROSS, EL, ERE, SRE, I;
precedence left         POWER, PRE, L;
precedence left         POST, ER, SR;
precedence left         _APPLICATION;
precedence left         INSTROKE, OUTSTROKE, NEXTSTROKE, NUMSTROKE;
precedence left         _RENAME;
precedence left         DOT;
precedence right        THETA;
precedence left         LSQUARE, RSQUARE;
precedence left         DECORWORD;

/// specifications /////////////////////////////////////////////////////////

//both anonymous and sectioned specifications have an optional narrative
//paragraph list to avoid precedence problems
specification ::=
        optNarrWordList:onwl paragraphList:pl
        {:
           // Apply Syntactic Transformation Rule 12.2.1.1
           Parent p = ozFactory_.createParent(TOOLKIT);
           ZSect sect = ozFactory_.createZSect("Specification", list(p), pl);
           if (onwl.size() > 0) {
             NarrPara ns = ozFactory_.createNarrPara(onwl);
             sect.getPara().add(0, ns);
           }
           Spec spec = ozFactory_.createSpec();
           spec.getSect().add(sect);
           RESULT = spec;
        :}
        |
        optNarrWordList:onwl sectionList:sl
        {:
           Spec spec = ozFactory_.createSpec();
           if (onwl.size() > 0) {
             NarrSect ns = ozFactory_.createNarrSect(onwl);
             spec.getSect().add(ns);
           }
           spec.getSect().addAll(sl);
           RESULT = spec;
        :}
        ;

optNarrWordList ::=
        //empty
        {: RESULT = list(); :}
	|
	narrWordList:nwl
	{: RESULT = nwl; :}
        ;

sectionList ::= 
        sectionList:sl section:s
        {: 
           sl.add(s);
           RESULT = sl;
        :}
        |
        section:s
        {: RESULT = list(s); :}
        ;

section ::=
        ZSECTION SECTION sectionName:n optParents:ops END paragraphList:pl
        {: 
           table_.endSection();
           RESULT = ozFactory_.createZSect(n, ops, pl);
        :}
        ;

//reduce this separately so we can set the current section
//in the operator table
sectionName ::=
        DECORWORD:dw
        {:
           table_.setSection(dw);
           RESULT = dw;
        :}
        ;

optParents ::=
        //empty
        {: RESULT = list(); :}
        |
        PARENTS parentList:pl
        {: RESULT = pl; :}
        |
        PARENTS
        {: RESULT = list(); :}
        ;

parentList ::=
        parentList:pl COMMA DECORWORD:dw
        {: 
           table_.addParent(dw);
           Parent p = ozFactory_.createParent(dw);
           pl.add(p);
           RESULT = pl;
        :}
        |
        DECORWORD:dw
        {: 
           table_.addParent(dw);
           Parent p = ozFactory_.createParent(dw);
           RESULT = list(p);
        :}
        ;


//paragraphs are divided into boxed and unboxed paragraphs. Unboxed paragraphs
//must have a newline after their definition, unless they are the final paragraph
//within ZED ... END tokens
paragraphList ::= 
        paragraphList:pl boxedParagraph:p 
        {: 
           pl.add(p);
           RESULT = pl;
        :}
        |
        paragraphList:pl unboxedParagraphList:upl
        {:
           pl.addAll(upl);
           RESULT = pl;
        :}
        |
        paragraphList:pl narrParagraph:np
        {:
           pl.add(np);
           RESULT = pl;
        :}
        |
        //empty
        {: RESULT = list(); :} %prec NL 
        ;


// 6.2 Global Paragraphs //////////////////////////////////////////////////////

boxedParagraph ::=
        axiomaticDefinition:ad
        {: RESULT = ad; :}
        |
        schema:s
        {: RESULT = s; :}
        |
        genericAxDefinition:gd
        {: RESULT = gd; :}
        |
        class:c
        {: RESULT = c; :}
        ;

narrParagraph ::=
        narrWordList:nwl
        {: RESULT = ozFactory_.createNarrPara(nwl); :}
        ;

unboxedParagraphList ::=
        ZED unboxedParagraphItemList:uil END
        {: RESULT = uil; :}
        ;

unboxedParagraphItemList ::=
        unboxedParagraphItemList:uil NL unboxedParagraphItem:ui
        {:
           uil.add(ui);
           RESULT = uil;
        :}
        |
        unboxedParagraphItem:ui
        {: RESULT = list(ui); :}
        ;

unboxedParagraphItem ::=
        basicTypeDefinition:btd
        {: RESULT = btd; :}
        |
        abbreviationDefinition:ad
        {: RESULT = ad; :}
        |
        freeTypeList:ftl
        {: RESULT = ozFactory_.createFreePara(ftl); :}
        |
        optFormalParameters:ofp CONJECTURE predicate1:p1
        {: RESULT = ozFactory_.createConjPara(ofp, p1); :}
        |
        operatorTemplate:ot
        {:
           table_.add(fix_, ot);
           fix_ = -1;
           RESULT = ot;
        :}
        ;

basicTypeDefinition ::= 
        LSQUARE nameList:nl RSQUARE
        {: RESULT = ozFactory_.createGivenPara(nl); :}
        ;

//according to the Z ISO, a schema is an axiomatic definition with
//ConstDecl(n, st) as the declaration
schema ::=
        SCH boxName:bn optFormalParameters:ofp schemaText:st END
        {: 
           SchExpr se = ozFactory_.createSchExpr(st);
           ConstDecl cd = ozFactory_.createConstDecl(bn, se);
           SchText stC = ozFactory_.createSchText(list(cd), null);
           RESULT = ozFactory_.createAxPara(ofp, stC, Box.SchBox);
        :}
        |
        GENSCH boxName:bn formalParameters:fp schemaText:st END
        {: 
           SchExpr se = ozFactory_.createSchExpr(st);
           ConstDecl cd = ozFactory_.createConstDecl(bn, se);
           SchText stC = ozFactory_.createSchText(list(cd), null);
           RESULT = ozFactory_.createAxPara(fp, stC, Box.SchBox);
        :}
        ;       

genericAxDefinition ::= 
        GENAX formalParameters:fp schemaText:st END
        {: RESULT = ozFactory_.createAxPara(fp, st, Box.AxBox); :}
        ;

optFormalParameters ::=
        formalParameters:fp
        {: RESULT = fp; :}
        |
        //empty
        {: RESULT = list(); :}
        ;

formalParameters ::= 
        LSQUARE nameList:nl RSQUARE
        {: RESULT = nl; :}
        ;

axiomaticDefinition ::= 
        AX schemaText:st END
        {: RESULT = ozFactory_.createAxPara(list(), st, Box.AxBox); :}
        ;

//according to the Z ISO standard, an abbreviation definition is to be
//treated as an axiomatic definition, with ConstDecl(dn,e)
abbreviationDefinition ::=
        declName:dn DEFEQUAL expression:e
        {:
           ConstDecl cd = ozFactory_.createConstDecl(dn, e);
           SchText st = ozFactory_.createSchText(list(cd), null);
           RESULT = ozFactory_.createAxPara(list(), st, Box.OmitBox);      
        :}
        |
        //TODO: check this. Should be name:n, but the toolkit won't parse
        //"\langle \_ \rangle [X] == .... without this
        declName:n formalParameters:fp DEFEQUAL expression:e
        {:
           ConstDecl cd = ozFactory_.createConstDecl(n, e);
           SchText st = ozFactory_.createSchText(list(cd), null);
           RESULT = ozFactory_.createAxPara(fp, st, Box.OmitBox);
        :}
        | 
        genName:gn DEFEQUAL expression:e
        {:
           ConstDecl cd = ozFactory_.createConstDecl(strListToDeclName(gn), e);
           SchText st = ozFactory_.createSchText(list(cd), null);
           RESULT = ozFactory_.createAxPara(list(), st, Box.OmitBox);
        :}
        ;


freeTypeList ::=
        freeTypeList:ftl ANDALSO freeTypeDefinition:ftd
        {: 
           ftl.add(ftd);
           RESULT = ftl;
        :}
        |
        freeTypeDefinition:ftd
        {: RESULT = list(ftd); :}
        ;


freeTypeDefinition ::=
        name:n DEFFREE branchList:bl
        {: RESULT = ozFactory_.createFreetype(n, bl); :}
        ;


branchList ::=
        branchList:bl BAR branch:b
        {:
           bl.add(b);
           RESULT = bl;
        :}
        |
        branch:b 
        {: RESULT = list(b); :}
        ;

branch ::=
        declName:dn
        {: RESULT = ozFactory_.createBranch(dn, null); :}
        |
        declName:dn LDATA expression:e RDATA
        {: RESULT = ozFactory_.createBranch(dn, e); :}
        ;

narrWordList ::=
        narrWordList:nwl TEXT:t
        {:
           nwl.add(t);
           RESULT = nwl;
        :}
        |
        TEXT:t
        {: RESULT = list(t); :}
        ;

// operator templates  /////////////////////////////////////////////////////

operatorTemplate ::=
        RELATION template:t
        {:
           OptempPara otp =
             ozFactory_.createOptempPara();
           otp.getWordOrOperand().addAll(t);
           otp.setCat(Cat.Relation);         
           RESULT = otp;
        :}
        |
        FUNCTION catTemplate:ct
        {:
           ct.setCat(Cat.Function);
           RESULT = ct;
        :}
        |
        GENERIC catTemplate:ct
        {:
           ct.setCat(Cat.Generic);
           RESULT = ct;
        :}
        ;

template ::=
        prefixTemplate:pt
        {: RESULT = pt; :}
        |
        postfixTemplate:pt
        {: RESULT = pt; :}
        |
        infixTemplate:it
        {: RESULT = it; :}
        |
        nofixTemplate:nt
        {: RESULT = nt; :}
        ;


catTemplate ::=
        prefixTemplate:pt
        {: 
           OptempPara otp = ozFactory_.createOptempPara();
           otp.getWordOrOperand().addAll(pt);
           RESULT = otp;
        :}
        |
        postfixTemplate:pt
        {: 
           OptempPara otp = ozFactory_.createOptempPara();
           otp.getWordOrOperand().addAll(pt);
           RESULT = otp;
        :}
        |
        NUMERAL:pr assoc:a infixTemplate:it
        {:
           OptempPara otp = ozFactory_.createOptempPara();
           otp.setPrec(pr);
           otp.getWordOrOperand().addAll(it);
           otp.setAssoc(a);
           RESULT = otp;
        :}
        |
        nofixTemplate:nt
        {:
           OptempPara otp = ozFactory_.createOptempPara();
           otp.getWordOrOperand().addAll(nt);
           RESULT = otp;
        :}
        ;


assoc ::=
        LEFTASSOC
        {: RESULT = Assoc.Left; :}
        |
        RIGHTASSOC
        {: RESULT = Assoc.Right; :}
        ;


prefixTemplate ::=
        LPAREN DECORWORD:n optWordOperandList:ol varg:v RPAREN
        {: 
           List l = list(n);
           l.addAll(ol);
           l.add(v);
           fix_ = OperatorTable.PREFIX;
           RESULT = l; 
        :}
	|
	LPAREN POWER:p varg:v RPAREN
	{:
           DeclName dn = ozFactory_.createDeclName(p, list(), null);
           List l = list(dn, v);
           fix_ = OperatorTable.PREFIX;
           RESULT = l;
	:}
        ;


postfixTemplate ::=
        LPAREN varg:v DECORWORD:n optWordOperandList:ol RPAREN
        {: 
           List l = list(v);
           l.add(n);
           l.addAll(ol);
           fix_ = OperatorTable.POSTFIX;
           RESULT = l; 
        :}
        ;


infixTemplate ::=
        LPAREN varg:v1 DECORWORD:n optWordOperandList:ol varg:v2 RPAREN
        {: 
           List l = list(v1);
           l.add(n);
           l.addAll(ol);
           l.add(v2);
           fix_ = OperatorTable.INFIX;
           RESULT = l; 
        :}
        ;


nofixTemplate ::=
        LPAREN DECORWORD:n optWordOperandList:ol RPAREN
        {: 
           List l = list(n);
           l.addAll(ol);
           fix_ = OperatorTable.NOFIX;
           RESULT = l;
        :}
        ;


optWordOperandList ::=
        //empty
        {: RESULT = list(); :}
        |
        optWordOperandList:wl templateTag:t DECORWORD:n
        {:
           wl.add(t);
           wl.add(n);
           RESULT = wl;
        :}
        ;

templateTag ::=
        varg:v
        {: RESULT = v; :}
        |
        LISTARG
        {: RESULT = ozFactory_.createOperand(Boolean.TRUE); :}
        ;

varg ::=
        ARG
        {: RESULT = ozFactory_.createOperand(Boolean.FALSE); :}
        ;


/// schema text ///////////////////////////////////////////

schemaText ::= 
        optDeclPart:odp BAR predicate:p
        {: RESULT = ozFactory_.createSchText(odp, p); :}
        |
        optDeclPart:odp
        {: RESULT = ozFactory_.createSchText(odp, null); :}
        ;

optDeclPart ::=
        //empty
        {: RESULT = list(); :}
        |
        declPart:dp
        {: RESULT = dp; :}
        ;

declPart ::=
        declaration:d
        {: RESULT = list(d); :}
        |
        declPart:dp sep:s declaration:d
        {:
           dp.add(d);
           RESULT = dp;
        :}
        ;

declaration ::=
        basicDeclaration:bd
        {: RESULT = bd; :}
        |
        expression:e
        {: RESULT = ozFactory_.createInclDecl(e); :}
        ;

expressionSchemaText ::=
	expression:e BAR predicate1:p1
	{:
           InclDecl id = ozFactory_.createInclDecl(e);
           RESULT = ozFactory_.createSchText(list(id), p1);
        :}
	|
	expression:e
	{:
           InclDecl id = ozFactory_.createInclDecl(e);
           RESULT = ozFactory_.createSchText(list(id), null);
        :}
	;

schemaTextNoExpression ::=
        declPart:dp BAR predicate1:p1
        {: RESULT = ozFactory_.createSchText(dp, p1); :}
        |
	BAR predicate1:p1
        {: RESULT = ozFactory_.createSchText(list(), p1); :}
        |
	declPart:dp sep declaration:d
	{:
           dp.add(d);
           RESULT = ozFactory_.createSchText(dp, null);
	:}
	|
	basicDeclaration:bd
	{: RESULT = ozFactory_.createSchText(list(bd), null); :}
        ;


basicDeclaration ::=
        declWordList:dwl COLON expression:e
        {: RESULT = ozFactory_.createVarDecl(dwl, e); :}
        |
        declName:dn DEFEQUAL expression:e
        {: RESULT = ozFactory_.createConstDecl(dn, e); :}
        ;       

sep ::=
        SEMICOLON
        {: RESULT = Op.Semi; :}
        |
        NL
        {: RESULT = Op.NL; :}
        ;

optSep ::=
        optNL
        |
        SEMICOLON
        ;

optNL ::=
        //empty
        |
        NL
        ;

/// word, declName, and refName lists ////////////////////////////////////

nameList ::=
        nameList:nl COMMA DECORWORD:dw
        {: 
           DeclName dn = localCreateDeclName(dw);
           nl.add(dn);
           RESULT = nl;
        :}
        |
        DECORWORD:dw
        {: 
           DeclName dn = localCreateDeclName(dw);
           RESULT = list(dn);
        :}
        ;

name ::=
        DECORWORD:dw
        {: RESULT = localCreateDeclName(dw); :}
        ;

boxName ::=
        BOXNAME:bn
        {: RESULT = localCreateDeclName(bn); :}
        ;

declWordList ::=
        declWordList:dwl COMMA DECLWORD:dw
        {: 
           DeclName dn = localCreateDeclName(dw);
           dwl.add(dn); 
           RESULT = dwl;
        :}
        |
        declWordList:dwl COMMA opName:on
        {: 
           dwl.add(strListToDeclName(on)); 
           RESULT = dwl;
        :}
        |
        opName:on
        {: RESULT = list(strListToDeclName(on)); :}
        |
        DECLWORD:dw
        {:
           DeclName dn = localCreateDeclName(dw);
           RESULT = list(dn); 
        :}
        ;


declNameList ::=
        declNameList:dnl COMMA declName:dn
        {:
           dnl.add(dn);
           RESULT = dnl;
        :}
        |
        declName:dn
        {: RESULT = list(dn); :}
        ;

declName ::=
        DECORWORD:dw
        {: 
           RESULT = localCreateDeclName(dw);
        :}
        |
        opName:on
        {:
           //because we return opNames as RefName instances, we must convert
           //to a DeclName
           RESULT = strListToDeclName(on);
        :}
        ;

refNameList ::=
        refNameList:rnl COMMA DECORWORD:dw
        {:
           RefName rn = localCreateRefName(dw);
           rnl.add(rn);
           RESULT = rnl;
        :}
        |
        refNameList:rnl COMMA INITWORD:iw
        {:
           RefName rn = localCreateRefName(iw);
           rnl.add(rn);
           RESULT = rnl;
        :}
        |
        DECORWORD:dw
        {:
           RefName rn = localCreateRefName(dw);
           RESULT = list(rn);
        :}
        |
        INITWORD:iw
        {:
           RefName rn = localCreateRefName(iw);
           RESULT = list(rn);
        :}
        ;

refName ::=
        DECORWORD:dw
        {: RESULT = localCreateRefName(dw); :}
        |
        INITWORD:iw
        {:
           RefName rn = localCreateRefName(iw);
           RESULT = rn;
        :}
        |
        LPAREN opName:on RPAREN
        {:
           RefName rn = ozFactory_.createRefName(strListToDeclName(on).getWord(),
                                                 strListToDeclName(on).getStroke(),
                                                 null);
           rn.getAnns().add(ozFactory_.createParenAnn());
           RESULT = rn;
        :}
        ;


opName ::=
        prefixName:pn
        {: RESULT = pn; :}
        |
        postfixName:pn
        {: RESULT = pn; :}
        |
        infixName:in
        {: RESULT = in; :}
        |
        nofixName:nn
        {: RESULT = nn; :}
        ;

prefixName ::=
        PRE:p varg
        {: RESULT = list(p, ARG_TOK); :}
        |
        PREP:p varg
        {: RESULT = list(p, ARG_TOK); :}
        |
        L:l optVargEsSsList:ouesl argEreOrSre:aeos varg
        {: 
           List pName = list(l);
           pName.addAll(ouesl);
           pName.addAll(aeos);
           pName.add(ARG_TOK);
           RESULT = pName;
        :}
        |
        LP:lp optVargEsSsList:ouesl argErepOrSrep:aeos varg
        {: 
           List pName = list(lp);
           pName.addAll(ouesl);
           pName.addAll(aeos);
           pName.add(ARG_TOK);
           RESULT = pName;
        :}
        ;

postfixName ::=
        varg POST:p
        {: RESULT = list(ARG_TOK, p); :}
        |
        varg POSTP:p
        {: RESULT = list(ARG_TOK, p); :}
        |
        varg EL:el optVargEsSsList:ouesl argErOrSr:aeos
        {:
           List pName = list(ARG_TOK);
           pName.add(el);
           pName.addAll(ouesl);
           pName.addAll(aeos);
           RESULT = pName;
        :}
        |
        varg ELP:elp optVargEsSsList:ouesl argErpOrSrp:aeos
        {:
           List pName = list(ARG_TOK);
           pName.add(elp);
           pName.addAll(ouesl);
           pName.addAll(aeos);
           RESULT = pName;
        :}
        ;


infixName ::=
        varg I:i varg
        {:
           List iName = list(ARG_TOK);
           iName.add(i);
           iName.add(ARG_TOK);
           RESULT = iName;
        :}
        |
        varg IP:ip varg
        {:
           List iName = list(ARG_TOK);
           iName.add(ip);
           iName.add(ARG_TOK);
           RESULT = iName;
        :}
        |
        varg EL:el optVargEsSsList:ouesl argEreOrSre:aeos varg
        {:
           List iName = list(ARG_TOK);
           iName.add(el);
           iName.addAll(ouesl);
           iName.addAll(aeos);
           iName.add(ARG_TOK);
           RESULT = iName;
        :}
        |
        varg ELP:elp optVargEsSsList:ouesl argErepOrSrep:aeos varg
        {:
           List iName = list(ARG_TOK);
           iName.add(elp);
           iName.addAll(ouesl);
           iName.addAll(aeos);
           iName.add(ARG_TOK);
           RESULT = iName;
        :}
        ;

nofixName ::=
        L:l optVargEsSsList:ouesl argErOrSr:aeos
        {:
           List nName = list(l);
           nName.addAll(ouesl);
           nName.addAll(aeos);
           RESULT = nName;
        :}
        |
        LP:lp optVargEsSsList:ouesl argErpOrSrp:aeos
        {:
           List nName = list(lp);
           nName.addAll(ouesl);
           nName.addAll(aeos);
           RESULT = nName;
        :}
        ;

optVargEsSsList ::=
        //empty
        {: RESULT = list(); :}
        |
        optVargEsSsList:ouesl argEsOrSs:aeos
        {: 
           ouesl.addAll(aeos);
           RESULT = ouesl;
        :}
        ;

argEsOrSs ::=
        ARG ES:es
        {: RESULT = list(ARG_TOK, es); :}
        |
        LISTARG SS:ss
        {: RESULT = list(LISTARG_TOK, ss); :}
        ;

argEreOrSre ::=
        ARG ERE:ere
        {: RESULT = list(ARG_TOK, ere); :}
        |
        LISTARG SRE:sre
        {: RESULT = list(LISTARG_TOK, sre); :}
        ;

argErepOrSrep ::=
        ARG EREP:erep
        {: RESULT = list(ARG_TOK, erep); :}
        |
        LISTARG SREP:srep
        {: RESULT = list(LISTARG_TOK, srep); :}
        ;

argErOrSr ::=
        ARG ER:er
        {: RESULT = list(ARG_TOK, er); :}
        |
        LISTARG SR:sr
        {: RESULT = list(LISTARG_TOK, sr); :}
        ;

argErpOrSrp ::=
        ARG ERP:erp
        {: RESULT = list(ARG_TOK, erp); :}
        |
        LISTARG SRP:srp
        {: RESULT = list(LISTARG_TOK, srp); :}
        ;

genName ::=
        prefixGenName:pgn
        {: RESULT = pgn; :}
        |
        postfixGenName:pgn
        {: RESULT = pgn; :}
        |
        infixGenName:ign
        {: RESULT = ign; :}
        |
        nofixGenName:ngn
        {: RESULT = ngn; :}
        ;

prefixGenName ::=
        PRE:pre DECORWORD:dw
        {: RESULT = list(pre, dw); :}
        |
        L:l optNameEsSsList:onesl DECORWORD:dw1 ereOrSre:eos DECORWORD:dw2
        {:
           List pgName = list(l);
           pgName.addAll(onesl);
           pgName.add(dw1);
           pgName.add(eos);
           pgName.add(dw2);
           RESULT = pgName; 
        :}
        ;

postfixGenName ::=
        DECORWORD:dw POST:p
        {: RESULT = list(dw, p); :}
        |
        DECORWORD:dw1 EL:el optNameEsSsList:onesl DECORWORD:dw2 erOrSr:eos
        {:
           List pgName = list(dw1);
           pgName.add(el);
           pgName.addAll(onesl);
           pgName.add(dw2);
           pgName.add(eos);
           RESULT = pgName;
         :}
        ;

infixGenName ::=
        DECORWORD:dw1 I:i DECORWORD:dw2
        {:
           List igName = list(dw1);
           igName.add(i);
           igName.add(dw2);
           RESULT = igName;
        :}
        |
        DECORWORD:dw1 EL:el optNameEsSsList:onesl DECORWORD:dw2 ereOrSre:eos DECORWORD:dw3
        {: 
           List igName = list(dw1);
           igName.add(el);
           igName.addAll(onesl);
           igName.add(dw2);
           igName.add(eos);
           igName.add(dw3);
           RESULT = igName;
        :}
        ;

nofixGenName ::=
        L:l optNameEsSsList:onesl DECORWORD:dw erOrSr:eos
        {:
           List ngName = list(l);
           ngName.addAll(onesl);
           ngName.add(dw);
           ngName.add(eos);
           RESULT = ngName;
        :}
        ;

optNameEsSsList ::=
        //empty
        {: RESULT = list(); :}
        |
        optNameEsSsList:ouesl DECORWORD:dw esOrSs:eos
        {:
           ouesl.add(dw);
           ouesl.add(eos);
           RESULT = ouesl;
        :}
        ;


esOrSs ::=
        ES:es
        {: RESULT = es; :}
        |
        SS:ss
        {: RESULT = ss; :}
        ;

ereOrSre ::=
        ERE:ere
        {: RESULT = ere; :}
        |
        SRE:sre
        {: RESULT = sre; :}
        ;

erOrSr ::=
        ER:er
        {: RESULT = er; :}
        |
        SR:sr
        {: RESULT = sr; :}
        ;

/// predicates and expressions ///////////////////////////////////////////////

predicate ::=
        predicate:lhs sep:s predicate1:rhs
        {: RESULT = ozFactory_.createAndPred(lhs, rhs, s); :}
        |
        predicate1:p1
        {: RESULT = p1; :}
        ;

predicate1 ::=
        term:t
        {: RESULT = pred(t); :}
        ;

expression ::=
        term:t
        {: RESULT = expr(t); :}
        ;

term ::=
        IF predicate1:p1 THEN term:te ELSE term:fe
        {: RESULT = ozFactory_.createCondExpr(p1, expr(te), expr(fe)); :}
        |
        ALL schemaText:st SPOT term:t
        {: 
           if (t instanceof Expr) {
             ForallExpr ee = ozFactory_.createForallExpr(st, expr(t));
             RESULT = ozFactory_.createExprPred(ee);
           }
           else {
             RESULT = ozFactory_.createForallPred(st, pred(t));
           }
        :}
        |
        EXI schemaText:st SPOT term:t
        {:
           if (t instanceof Expr) {
             ExistsExpr ee = ozFactory_.createExistsExpr(st, expr(t));
             RESULT = ozFactory_.createExprPred(ee);
           }
           else {
             RESULT = ozFactory_.createExistsPred(st, pred(t));
           }
        :}
        |
        EXIONE schemaText:st SPOT term:t
        {: 
           if (t instanceof Expr) {
             Exists1Expr ee = ozFactory_.createExists1Expr(st, expr(t));
             RESULT = ozFactory_.createExprPred(ee);
           }
           else {
             RESULT = ozFactory_.createExists1Pred(st, pred(t));
           }
        :}
        |
        LAMBDA schemaText:st SPOT term:t
        {: RESULT = ozFactory_.createLambdaExpr(st, expr(t)); :} %prec LAMBDA
        |
        MU schemaText:st SPOT term:t
        {: RESULT = ozFactory_.createMuExpr(st, expr(t)); :} %prec MU
        |
        LET letDefinitionList:ldl SPOT term:t 
        {: 
           SchText st = ozFactory_.createSchText(ldl, null);
           RESULT = ozFactory_.createLetExpr(st, expr(t));
        :} %prec LET
        |
        term:lhs IFF term:rhs
        {:
          if (lhs instanceof Expr && rhs instanceof Expr) {
            RESULT = ozFactory_.createIffExpr(expr(lhs), expr(rhs)); 
          }
          else {
            RESULT = ozFactory_.createIffPred(pred(lhs), pred(rhs));
          }
        :}
        |
        term:lhs IMP term:rhs
        {:
          if (lhs instanceof Expr && rhs instanceof Expr) {
            RESULT = ozFactory_.createImpliesExpr(expr(lhs), expr(rhs)); 
          }
          else {
            RESULT = ozFactory_.createImpliesPred(pred(lhs), pred(rhs));
          }
        :}
        |
        term:lhs OR term:rhs
        {:
          if (lhs instanceof Expr && rhs instanceof Expr) {
            RESULT = ozFactory_.createOrExpr(expr(lhs), expr(rhs)); 
          }
          else {
            RESULT = ozFactory_.createOrPred(pred(lhs), pred(rhs));
          }
        :}
        |
        term:lhs AND term:rhs
        {:
          if (lhs instanceof Expr && rhs instanceof Expr) {
            RESULT = ozFactory_.createAndExpr(expr(lhs), expr(rhs)); 
          }
          else {
            RESULT = ozFactory_.createAndPred(pred(lhs), pred(rhs), Op.And);
          }
        :}
        |
        NOT term:t
        {:
          if (t instanceof Expr) {
            RESULT = ozFactory_.createNegExpr(expr(t));
          }
          else {
            RESULT = ozFactory_.createNegPred(pred(t));
          }
        :}
        |
        term:lhs ZCOMP term:rhs
        {: RESULT = ozFactory_.createCompExpr(expr(lhs), expr(rhs)); :}
        |
        term:lhs ZPIPE term:rhs
        {: RESULT = ozFactory_.createPipeExpr(expr(lhs), expr(rhs)); :}
        |
        term:t ZHIDE LPAREN refNameList:rnl RPAREN
        {: RESULT = ozFactory_.createHideExpr(expr(t), rnl); :}
        |
        term:lhs ZPROJ term:rhs
        {: RESULT = ozFactory_.createProjExpr(expr(lhs), expr(rhs)); :}
        |
        ZPRE term:t
        {: RESULT = ozFactory_.createPreExpr(expr(t)); :}
        |
        POWER term:t
        {: RESULT = ozFactory_.createPowerExpr(expr(t)); :}
        |
        prodExpressionList:pel
        {: RESULT = ozFactory_.createProdExpr(pel); :}
        |
        relation:rel
        {: RESULT = rel; :}
        |
        application:a
        {: RESULT = a; :}
        |
        func_appl:fa
        {: RESULT = fa; :}
        ;

func_appl ::=
        func_appl:fa inner_term:it
        {:
           RESULT = ozFactory_.createApplExpr(expr(fa), expr(it), Boolean.FALSE);
        :} %prec _APPLICATION
        |
        inner_term:it
        {: RESULT = it; :} %prec _APPLICATION
        ;

inner_term ::=
        LBRACE optExpressionList:oel RBRACE
        {: RESULT = ozFactory_.createSetExpr(oel); :}
        |
        LBRACE schemaTextNoExpression:stne SPOT expression:e RBRACE
        {: RESULT = ozFactory_.createSetCompExpr(stne, e); :}
	|
	LBRACE expressionSchemaText:est SPOT expression:e RBRACE
        {: RESULT = ozFactory_.createSetCompExpr(est, e); :}
        |
        LBRACE schemaTextNoExpression:stne RBRACE
        {: RESULT = ozFactory_.createSetCompExpr(stne, null); :}
        |
        LPAREN expressionList:el COMMA expression:e RPAREN
        {: 
           el.add(e);
           RESULT = ozFactory_.createTupleExpr(el);
        :}
        |
        LPAREN MU schemaText:st RPAREN
        {:
           MuExpr me = ozFactory_.createMuExpr(st, null);
           me.getAnns().add(ozFactory_.createParenAnn());
           RESULT = me;
        :}
        |
        nofixRel:nfr
        {: RESULT = nfr; :} %prec ERP
        |
        nofixApp:nfa
        {: RESULT = nfa; :} %prec _APPLICATION
        |
        LBIND optNameExprPairList:onepl RBIND
        {: RESULT = ozFactory_.createBindExpr(onepl); :}
        |
        LSQUARE schemaTextNoExpression:stne RSQUARE
        {: RESULT = ozFactory_.createSchExpr(stne); :}
        |
        inner_term:it DOT refName:rn
        {: RESULT = ozFactory_.createBindSelExpr(expr(it), rn); :}
        |
        inner_term:it DOT NUMERAL:n
        {: RESULT = ozFactory_.createTupleSelExpr(expr(it), n); :}
        |
        inner_term:it stroke:s
        {: RESULT = ozFactory_.createDecorExpr(expr(it), s); :}
        |
        THETA inner_term:it optStrokeList:sl
        {: RESULT = ozFactory_.createThetaExpr(expr(it), sl); :}
        |
        inner_term:rn LSQUARE expressionList:el RSQUARE
        {: RESULT = ozFactory_.createRefExpr(refname(rn), el, Boolean.TRUE); :}
        |
        inner_term:it LSQUARE renameList:rnl RSQUARE
        {: RESULT = ozFactory_.createRenameExpr(expr(it), rnl); :} %prec _RENAME
        |
        NUMERAL:n
        {: RESULT = ozFactory_.createNumExpr(new BigInteger(n.toString())); :}
        |
        LPAREN term:t RPAREN
        {:
           t.getAnns().add(ozFactory_.createParenAnn());
           RESULT = t;
        :}
        |
        refName:rn
        {: RESULT = ozFactory_.createRefExpr(rn, list(), Boolean.TRUE); :}
        |
        TRUE
        {: RESULT = ozFactory_.createTruePred(); :}
        |
        FALSE
        {: RESULT = ozFactory_.createFalsePred(); :}
        ;

prodExpressionList ::=
        term:t1 CROSS term:t2
        {: RESULT = list(t1, t2); :}
        |
        prodExpressionList:pel CROSS term:t
        {:
           pel.add(t);
           RESULT = pel;
        :}
        ;

optExpressionList ::=
        //empty
        {: RESULT = list(); :}
        |
        expressionList:el
        {: RESULT = el; :}
        ;

expressionList ::=
        expressionList:el COMMA expression:e
        {:      
           el.add(e);
           RESULT = el;
        :} 
        |
        expression:e
        {: RESULT = list(e); :}
        ;


optNameExprPairList ::=
        //empty
        {: RESULT = list(); :}
        |
        nameExprPairList:nepl
        {: RESULT = nepl; :}
        ;

nameExprPairList ::=
        nameExprPairList:nepl COMMA nameExprPair:nep
        {:      
           nepl.add(nep);
           RESULT = nepl;
        :} 
        |
        nameExprPair:nep
        {: RESULT = list(nep); :}
        ;       

nameExprPair ::=
        declName:dn DEFEQUAL expression:e
        {: RESULT = ozFactory_.createNameExprPair(dn, e); :}
        ;


letDefinitionList ::=
        letDefinitionList:ldl SEMICOLON letDefinition:ld
        {:
           ldl.add(ld);
           RESULT = ldl;
        :}
        |
        letDefinition:ld
        {: RESULT = list(ld); :}
        ;       

letDefinition ::=
        declName:n DEFEQUAL expression:e
        {: RESULT = ozFactory_.createConstDecl(n,e); :}
        ;

optStrokeList ::=
        optStrokeList:sl stroke:s
        {:
           sl.add(s);
           RESULT = sl;
        :}
        |
        //empty
        {: RESULT = list(); :} %prec INSTROKE
        ;

stroke ::=
        INSTROKE
        {: RESULT = ozFactory_.createInStroke(); :}
        |
        OUTSTROKE
        {: RESULT = ozFactory_.createOutStroke(); :}
        |
        NEXTSTROKE
        {: RESULT = ozFactory_.createNextStroke(); :}
        |
        NUMSTROKE:ns
        {: RESULT = ozFactory_.createNumStroke(ns); :}
        ;

renameList ::= 
        renameList:rl COMMA rename:rn
        {:
           rl.add(rn);
           RESULT = rl;
        :}
        |
        rename:rn  
        {: RESULT = list(rn); :}
        ;    

rename ::=
        refName:lhs SLASH declName:rhs
        {: RESULT = ozFactory_.createNameNamePair(lhs, rhs); :}
        ;


//nofix relation is not included here because it has a different precedence
//than the other types of relation
relation ::=
        prefixRel:pr
        {: RESULT = pr; :}
        |
        postfixRel:pr
        {: RESULT = pr; :}
        |
        infixRel:ir
        {: RESULT = ir; :}
        ;

//according to the ISO standard, op e1 is equivalent to
//e mem op
prefixRel ::=
        PREP:p term:t
        {: RESULT = localCreateMemPred(list(p), list(expr(t))); :}
        |
        LP:lp expSep:es term:t1 EREP:erep term:t2
        {: 
           List opName = list(lp);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(erep);

           List expList = list();
           addExpSepExp(expList, es);
           expList.add(expr(t1));
           expList.add(expr(t2));
           RESULT = localCreateMemPred(opName, expList);
        :}
        |
        LP:lp expSep:es optExpressionList:oel SREP:srep term:t
        {:
           List opName = list(lp);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(srep);

           List expList = list();
           addExpSepExp(expList, es);
           expList.addAll(oel);
           expList.add(expr(t));
           RESULT = localCreateMemPred(opName, expList);
        :}
        ;

postfixRel ::=
        term:t POSTP:p
        {: RESULT = localCreateMemPred(list(p), list(expr(t))); :}
        |
        term:t1 ELP:elp expSep:es term:t2 ERP:erp
        {:
           List opName = list(ARG_TOK);
           opName.add(elp);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(erp);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.add(expr(t2));
           RESULT = localCreateMemPred(opName, expList);
        :}
        |
        term:t ELP:elp expSep:es optExpressionList:oel SRP:srp
        {:
           List opName = list(ARG_TOK);
           opName.add(elp);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(srp);

           List expList = list(expr(t));
           addExpSepExp(expList, es);
           expList.addAll(oel);
           RESULT = localCreateMemPred(opName, expList);
        :}
        ;

infixRel ::=
        infixChainRel:icr
        {: RESULT = icr; :}
        |
        term:t1 ELP:elp expSep:es term:t2 EREP:erep term:t3
        {:
           List opName = list(ARG_TOK);
           opName.add(elp);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(erep);
           opName.add(ARG_TOK);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.add(expr(t2));
           expList.add(expr(t3));
           RESULT = localCreateMemPred(opName, expList);
        :}
        |
        term:t1 ELP:elp expSep:es optExpressionList:oel SREP:erep term:t2
        {:
           List opName = list(ARG_TOK);
           opName.add(elp);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(erep);
           opName.add(ARG_TOK);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.addAll(oel);
           expList.add(expr(t2));
           RESULT = localCreateMemPred(opName, expList);
        :}
        ;

infixChainRel ::=
        term:lhs MEM term:rhs
        {:
           previousChain_ = LatexSym.MEM;
           RESULT = ozFactory_.createMemPred(expr(lhs), expr(rhs), Boolean.TRUE);
        :}
        |
        term:lhs EQUALS term:rhs
        {:
           previousChain_ = LatexSym.EQUALS;
           SetExpr se = ozFactory_.createSetExpr(list(expr(rhs)));
           RESULT = ozFactory_.createMemPred(expr(lhs), se, Boolean.TRUE);
        :}
        |
        term:lhs IP:ip term:rhs
        {:
           previousChain_ = LatexSym.IP;
           List opName = list(ARG_TOK);
           opName.add(ip);
           opName.add(ARG_TOK);
           RESULT = localCreateMemPred(opName, list(expr(lhs), expr(rhs)));
        :}
        |
        //apply syntactic transformation rule 12.2.10.3
        infixChainRel:icr MEM term:rhs
        {:
           RESULT = transformChainRel(icr, LatexSym.MEM, expr(rhs));
           previousChain_ = LatexSym.MEM;
        :}
        |
        //apply syntactic transformation rule 12.2.10.3
        infixChainRel:icr EQUALS term:rhs
        {:
           RESULT = transformChainRel(icr, LatexSym.EQUALS, expr(rhs));
           previousChain_ = LatexSym.EQUALS;
        :}
        |
        //apply syntactic transformation rule 12.2.10.3
        infixChainRel:icr IP:ip term:rhs
        {:
           RESULT = transformChainRelInfix(icr, expr(rhs), ip);
           previousChain_ = LatexSym.IP;
        :}
        ;

nofixRel ::=
        LP:lp expSep:es term:t ERP:erp
        {:
           List opName = list(ARG_TOK);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(erp);

           List expList = list();
           addExpSepExp(expList, es);
           expList.add(expr(t));
           RESULT = localCreateMemPred(opName, expList);
        :}
        |
        LP:lp expSep:es optExpressionList:oel SRP:srp
        {:
           List opName = list(ARG_TOK);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(srp);

           List expList = list();
           addExpSepExp(expList, es);
           expList.addAll(oel);
           RESULT = localCreateMemPred(opName, expList);
        :}
        ;


//nofix application is not included here because it has a different
//precedence to the other types of application
application ::=
        prefixApp:pa
        {: RESULT = pa; :}
        |
        postfixApp:pa
        {: RESULT = pa; :}
        |
        infixApp:ia
        {: RESULT = ia; :}
        ;

prefixApp ::=
        PRE:pre term:t
        {: RESULT = localCreateApplExpr(list(pre), list(expr(t))); :}
        |
        L:l expSep:es term:t1 ERE:ere term:t2
        {:
           List opName = list(l);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(ere);
           opName.add(ARG_TOK);

           List expList = list();
           addExpSepExp(expList, es);
           expList.add(expr(t1));
           expList.add(expr(t2));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        |
        L:l expSep:es optExpressionList:oel SRE:sre term:t
        {:
           List opName = list(l);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(sre);
           opName.add(ARG_TOK);

           List expList = list();
           addExpSepExp(expList, es);
           expList.addAll(oel);
           expList.add(expr(t));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        ;

postfixApp ::=
        term:t POST:post
        {: RESULT = localCreateApplExpr(list(post), list(expr(t))); :}
        |
        term:t1 EL:el expSep:es term:t2 ER:er
        {:
           List opName = list(ARG_TOK);
           opName.add(el);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(er);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.add(expr(t2));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        |
        term:t EL:el expSep:es optExpressionList:oel SR:sr
        {:
           List opName = list(ARG_TOK);
           opName.add(el);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(sr);

           List expList = list(expr(t));
           addExpSepExp(expList, es);
           expList.addAll(oel);
           RESULT = localCreateApplExpr(opName, expList);
        :}
        ;

infixApp ::=
        term:lhs I:i term:rhs
        {: 
           List opName = list(ARG_TOK);
           opName.add(i);
           opName.add(ARG_TOK);
           RESULT = localCreateApplExpr(opName, list(lhs, rhs));
        :}
        |
        term:t1 EL:el expSep:es term:t2 ERE:ere term:t3
        {:
           List opName = list(ARG_TOK);
           opName.add(el);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(ere);
           opName.add(ARG_TOK);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.add(expr(t2));
           expList.add(expr(t3));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        |
        term:t1 EL:el expSep:es optExpressionList:oel SRE:sre term:t2
        {:
           List opName = list(ARG_TOK);
           opName.add(el);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(sre);
           opName.add(ARG_TOK);

           List expList = list(expr(t1));
           addExpSepExp(expList, es);
           expList.addAll(oel);
           expList.add(expr(t2));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        ;

nofixApp ::=
        L:l expSep:es term:t ER:er
        {:
           List opName = list(l);
           addExpSepName(opName, es);
           opName.add(ARG_TOK);
           opName.add(er);

           List expList = list();
           addExpSepExp(expList, es);
           expList.add(expr(t));
           RESULT = localCreateApplExpr(opName, expList);
        :}
        |
        L:l expSep:es optExpressionList:oel SR:sr
        {:
           List opName = list(l);
           addExpSepName(opName, es);
           opName.add(LISTARG_TOK);
           opName.add(sr);

           List expList = list();
           addExpSepExp(expList, es);
           expList.addAll(oel);
           RESULT = localCreateApplExpr(opName, expList);
        :}
        ;

expSep ::=
        //empty
        {: RESULT = list(); :} %prec I
        |
        expSep:exs term:t ES:es
        {:
           exs.add(list(expr(t)));
           exs.add(es);
           RESULT = exs;
        :}
        |
        expSep:exs expressionList:el SS:ss
        {:
           exs.add(el);
           exs.add(ss);
           RESULT = exs;
        :}
        ;


/// Object-Z constructs ///////////////////////////////////////////////

class ::= 
        CLASS boxName:bn
            classFormalParameters:fp
            visibilityList:vl
            inheritedList:il
            localDefinitionBlock:ldb
            state:s
            initialState:is
            operationList:ol
        END
        {: 
          RESULT = ozFactory_.createClassPara(bn, fp, vl, il, ldb, s, is, ol);
        :}
        ;

classFormalParameters ::=
        optFormalParameters:fp
        {: RESULT = ozFactory_.createFormalParameters(fp); :}
        ;


operationList ::=
        //empty
        {: RESULT = list(); :}
        |
        operationList:ol optNL operation:o
        {: 
           ol.add(o); 
           RESULT = ol;
        :}
        ;


/// class paragraphs ///////////////////////////////////////////////

visibilityList ::=
        //empty
        {: RESULT = null; :}
        |
        visibility LPAREN refNameList:rnl RPAREN optNL
        {: RESULT = ozFactory_.createRefNameList(rnl); :}
        ;

visibility ::=
        VISIBILITY
        |
        ZPROJ %prec VISIBILITY
        ;

inheritedList ::=
        //empty
        {: RESULT = list(); :}
        |
        INHERITS inheritedClassList:icl optNL
        {: RESULT = icl; :}
        ;

inheritedClassList ::=
        inheritedClassList:icl COMMA inheritedClass:ic
        {:
           icl.add(ic);
           RESULT = icl;
        :}
        |
        inheritedClass:ic
        {: RESULT = list(ic); :}
        ;

inheritedClass ::=
        refName:rn optActualParameters:oap optRenameExpression:ore
        {: RESULT = ozFactory_.createInheritedClass(rn, oap, ore); :}
        ;

optActualParameters ::=
        //empty
        {: RESULT = null; :}
        |
        LSQUARE expressionList:el RSQUARE
        {: RESULT = ozFactory_.createActualParameters(el); :}
        ;

optRenameExpression ::=
        //empty
        {: RESULT = null; :}
        |
        LSQUARE renameList:rnl  RSQUARE
        {: ozFactory_.createRenameExpr(null, rnl); :}
        ;

localDefinitionBlock ::=
        // empty
        {: RESULT = null; :}
        |
        localDefinitionList:ldl
        {:
           //get the list of definitions and put them in lists depending on
           //their type...
           List axParaList = list();
           List freeTypeList = list();
           List basicTypeList = list();

           for (Iterator it = ldl.iterator(); it.hasNext(); ) {
               Object item = it.next();

               if (item instanceof AxPara) {
                   axParaList.add(item);
               }
               else if (item instanceof Freetype) {
                   freeTypeList.add(item);
               }
               else if (item instanceof GivenPara) {
                   basicTypeList.add(item);
               }
           }
           RESULT = ozFactory_.createLocalDef(basicTypeList, axParaList, freeTypeList);
        :}
        ;


localDefinitionList ::=
        localDefinitionList:ldl boxedLocalDefinition:bld
        {:
           ldl.add(bld);
           RESULT = ldl;
        :}
        |
        localDefinitionList:ldl unboxedLocalDefinitionBlock:uldb
        {: 
           ldl.addAll(uldb);
           RESULT = ldl;
        :}
	|
        boxedLocalDefinition:bld
	{: RESULT = list(bld); :}
        |
        unboxedLocalDefinitionBlock:uldb
        {: RESULT = uldb; :}
        ;

boxedLocalDefinition ::=
        axiomaticDefinition:ad
        {: RESULT = ad; :}
        |
        genericAxDefinition:gad
        {: RESULT = gad; :}
        ;

unboxedLocalDefinitionBlock ::=
        ZED unboxedLocalDefinitionList:uldl END
        {: RESULT = uldl; :}
        ;

unboxedLocalDefinitionList ::=
        unboxedLocalDefinitionList:uldl NL unboxedLocalDefinition:uld
        {:
           uldl.add(uld);
           RESULT = uldl;
        :}
        |
        unboxedLocalDefinition:uld
        {: RESULT = list(uld); :}
        ;

unboxedLocalDefinition ::=
        basicTypeDefinition:gtd
        {: RESULT = gtd; :}
        |
        freeTypeList:ftl
        {: RESULT = ozFactory_.createFreePara(ftl); :}
        |
        abbreviationDefinition:ad
        {: RESULT = ad; :}
        ;

state ::=
        // empty
        {: RESULT = ozFactory_.createState(); :}
        |
        STATE stateDecl:sd BAR predicate:p END
        {:
           List dp = (List) sd.get(0);
           SecondaryAttributes sa = (SecondaryAttributes) sd.get(1);
           RESULT = ozFactory_.createState(dp, sa, list(p));
        :}
        |
        STATE stateDecl:sd END
        {:
           List dp = (List) sd.get(0);
           SecondaryAttributes sa = (SecondaryAttributes) sd.get(1);
           RESULT = ozFactory_.createState(dp, sa, list());
        :}
        | 
        LSQUARE stateDecl:sd BAR predicate:p RSQUARE
        {:
           List dp = (List) sd.get(0);
           SecondaryAttributes sa = (SecondaryAttributes) sd.get(1);
           RESULT = ozFactory_.createState(dp, sa, list(p));
        :}
        |
        LSQUARE stateDecl:sd RSQUARE
        {:
           List dp = (List) sd.get(0);
           SecondaryAttributes sa = (SecondaryAttributes) sd.get(1);
           RESULT = ozFactory_.createState(dp, sa, list());
        :}
        ;


//return a list with 2 elements: the declPart and the secondaryDecl
stateDecl ::=
        optDeclPart:odp
        {: RESULT = list(odp, null); :}
        |
        secondaryDecl:sd
        {: RESULT = list(list(), sd); :}
        |
        declPart:dp sep secondaryDecl:sd
        {: RESULT = list(dp, sd); :}
        ;

secondaryDecl ::=
        DELTA sep declPart:dp
        {: RESULT = ozFactory_.createSecondaryAttributes(dp); :}
        ;

initialState ::=
        // empty
        {: RESULT = ozFactory_.createInitialState(); :}
        |
        INIT predicate:p END
        {: RESULT = ozFactory_.createInitialState(list(p)); :}
        |
        INITWORD DEFEQUAL LSQUARE predicate:p RSQUARE
        {: RESULT = ozFactory_.createInitialState(list(p)); :}
        ;

operation ::=
        SCH boxName:bn operationBox:ob END
        {: RESULT = ozFactory_.createOperation(bn, ob); :}
        |
        OPSCH boxName:bn operationBox:ob END
        {: RESULT = ozFactory_.createOperation(bn, ob); :}
        |
        declName:dn DEFEQUAL operationExpression:oe
        {: RESULT = ozFactory_.createOperation(dn, oe); :}
        ;

operationBox ::=
        deltaList:dl sep:s declPart:dp BAR predicate:p
        {: RESULT = ozFactory_.createOperationBox(dl, dp, list(p)); :}
        | 
        deltaList:dl sep:s declPart:dp
        {: RESULT = ozFactory_.createOperationBox(dl, dp, list()); :}
        |        
        deltaList:dl BAR predicate:p  
        {: RESULT = ozFactory_.createOperationBox(dl, list(), list(p)); :}
        | 
        deltaList:dl
        {: RESULT = ozFactory_.createOperationBox(dl, list(), list()); :}
        | 
        declPart:dp BAR predicate:p
        {: RESULT = ozFactory_.createOperationBox(null, dp, list(p)); :}
        | 
        declPart:dp
        {: RESULT = ozFactory_.createOperationBox(null, dp, list()); :}
        ;


operationExpression ::=
        DCNJ mainOpExpression:moe
        {: RESULT = ozFactory_.createDistConjOpExpr(moe); :}
        |
        DGCH mainOpExpression:moe
        {: RESULT = ozFactory_.createDistChoiceOpExpr(moe); :}
        |
        DSQC mainOpExpression:moe
        {: RESULT = ozFactory_.createDistSeqOpExpr(moe); :}
        |
        operationExpression1:oe1
        {: RESULT = oe1; :}
        ;

mainOpExpression ::=
        schemaText:st SPOT operationExpression:oe
        {: RESULT = ozFactory_.createMainOpExpr(st, oe); :}
        ;

operationExpression1 ::=
        LSQUARE deltaList:dl optSep schemaText:st RSQUARE
        {: RESULT = ozFactory_.createBasicOpExpr(dl, st); :}
        |
        opPromotionLhs:oplhs DOT refName:rn
        {: RESULT = ozFactory_.createOpPromotionExpr(oplhs, rn); :}
        |
        refName:rn
        {: RESULT = ozFactory_.createOpPromotionExpr(null, rn); :}
        |
        operationExpression1:oe1 LSQUARE renameList:rnl RSQUARE
        {: RESULT = ozFactory_.createRenameOpExpr(oe1, rnl); :}
        |
        operationExpression1:oe1 ZHIDE LPAREN declNameList:onl RPAREN
        {: RESULT = ozFactory_.createHideOpExpr(oe1, onl);:}
        |
        operationExpression1:lhs AND operationExpression1:rhs
        {: RESULT = ozFactory_.createConjOpExpr(lhs, rhs); :}
        |
        operationExpression1:lhs PARALLEL operationExpression1:rhs
        {: RESULT = ozFactory_.createParallelOpExpr(lhs, rhs); :}
        |
        operationExpression1:lhs ASSOCPARALLEL operationExpression1:rhs
        {: RESULT = ozFactory_.createAssoParallelOpExpr(lhs, rhs); :}
        |
        operationExpression1:lhs GCH operationExpression1:rhs
        {: RESULT = ozFactory_.createExChoiceOpExpr(lhs, rhs); :}
        |
        operationExpression1:lhs ZCOMP operationExpression1:rhs
        {: RESULT = ozFactory_.createSeqOpExpr(lhs, rhs); :}
        |
        operationExpression1:lhs SPOT operationExpression1:rhs
        {: RESULT = ozFactory_.createScopeEnrichOpExpr(lhs, rhs); :}
        |
        LPAREN operationExpression:oe RPAREN
        {:
           oe.getAnns().add(ozFactory_.createParenAnn());
           RESULT = oe;
        :}
        ;

opPromotionLhs ::=
        refName:rn
        {: RESULT = ozFactory_.createRefExpr(rn, list(), Boolean.TRUE); :}
        |
        opPromotionLhs:oplhs DOT refName:rn
        {: RESULT = ozFactory_.createBindSelExpr(oplhs, rn); :}
        ;

deltaList ::=
        //empty
        {: RESULT = null; :}
        |
        DELTA LPAREN refNameList:rnl RPAREN
        {: RESULT = ozFactory_.createRefNameList(rnl); :}
        ;
